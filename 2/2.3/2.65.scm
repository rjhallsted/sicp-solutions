(load "binary-trees.scm")

(define (tree->list tree)
  (if (null? tree)
      '()
      (append (tree->list (left-branch tree))
              (cons (entry tree)
                    (tree->list (right-branch tree))))))

(define (list->tree elements)
  (car (partial-tree elements (length elements))))

(define (partial-tree elts n)
  (if (= n 0)
      (cons '() elts)
      (let ((left-size (quotient (- n 1) 2)))
        (let ((left-result (partial-tree elts left-size)))
          (let ((left-tree (car left-result))
                (non-left-elts (cdr left-result))
                (right-size (- n (+ left-size 1))))
            (let ((this-entry (car non-left-elts))
                  (right-result (partial-tree (cdr non-left-elts)
                                              right-size)))
              (let ((right-tree (car right-result))
                    (remaining-elts (cdr right-result)))
                (cons (make-tree this-entry left-tree right-tree)
                      remaining-elts))))))))

(define (balance-tree tree)
    (list->tree (tree->list tree)))

(define (union-set a b)
    (define (union-set-inner set1 set2)
        (cond ((null? set1) set2)
            ((null? set2) set1)
            (else
                (let ((x1 (car set1)) (x2 (car set2)))
                    (cond ((= x1 x2)
                            (cons x1 (union-set-inner (cdr set1) (cdr set2))))
                        ((< x1 x2)
                            (cons x1 (union-set-inner (cdr set1) set2)))
                        ((< x2 x1)
                            (cons x2 (union-set-inner set1 (cdr set2)))))))))
    (list->tree (union-set-inner (tree->list a) (tree->list b))))

(define (intersection-set a b)
    (define (intersection-set-inner set1 set2)
        (if (or (null? set1) (null? set2))
            '()    
            (let ((x1 (car set1)) (x2 (car set2)))
                (cond ((= x1 x2)
                        (cons x1
                            (intersection-set-inner (cdr set1)
                                                    (cdr set2))))
                    ((< x1 x2)
                        (intersection-set-inner (cdr set1) set2))
                    ((< x2 x1)
                        (intersection-set-inner set1 (cdr set2)))))))
    (list->tree (intersection-set-inner (tree->list a) (tree->list b))))
                              

(define tree-a (list->tree '(1 3 5 7 9 11 12 15 18 23)))
(define tree-b (list->tree '(2 4 6 8 10 12 15 18)))

(display (union-set tree-a tree-b)) (newline)
(display (intersection-set tree-a tree-b)) (newline)